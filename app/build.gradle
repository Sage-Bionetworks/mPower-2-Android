apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.butterknife'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "org.sagebionetworks.research.mpower"
        minSdkVersion 19
        targetSdkVersion 28

        // this is not the final version code, product flavors add to it
        versionCode 55
        versionName "0.13.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        resConfigs "en" // english only

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['autovaluegson.mutableAdaptersWithDefaultSetters': 'true']
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    flavorDimensions "userRole"

    productFlavors {
        internal {
            versionCode 1 + 2 * android.defaultConfig.versionCode
            versionNameSuffix "-internal"
            dimension "userRole"
        }
        participant {
            versionCode 2 * android.defaultConfig.versionCode
            dimension "userRole"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    kotlinOptions {
        // Should target '1.8', but there is currently a bug on Android SDK < 24 that was crashing login
        // See https://stackoverflow.com/questions/45935788/nosuchmethoderror-java-lang-long-hashcode
        jvmTarget = '1.6'
    }
}
androidExtensions {
    experimental = true
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'

    api 'com.google.guava:guava:27.0.1-android'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Local projects
    implementation project(':sageresearch-mpower')
    implementation project(':researchstack-mpower')

    implementation "org.sagebionetworks.research:data:$sage_research_version"
    implementation "org.sagebionetworks.research:mobile-ui:$sage_research_version"

    // ButterKnife
    implementation "com.jakewharton:butterknife:$butterknife_version"
    kapt "com.jakewharton:butterknife-compiler:$butterknife_version"

    // Logging
    implementation 'org.slf4j:slf4j-api:1.7.21'
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation 'com.github.tony19:logback-android-classic:1.1.1-6', {
        exclude group: 'com.google.android', module: 'android'
    }
    testImplementation 'org.slf4j:slf4j-simple:1.7.21'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.ryanharter.auto.value:auto-value-parcel-adapter:0.2.6'
    implementation "com.ryanharter.auto.value:auto-value-gson-annotations:$auto_value_gson_version"
    kapt "com.google.auto.value:auto-value:$auto_value_version"
    kapt 'com.ryanharter.auto.value:auto-value-parcel:0.2.6'
    kapt "com.ryanharter.auto.value:auto-value-gson:$auto_value_gson_version"
    kapt "com.google.auto.value:auto-value-annotations:$auto_value_version" // include again for auto-value-gson

    // Dagger
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'android.arch.lifecycle:extensions:1.1.1'

    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:support-vector-drawable:$support_library_version"
    implementation "com.android.support:support-v4:$support_library_version"
    implementation "com.android.support:support-annotations:$support_library_version"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"

    // room db functionality
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    kapt 'android.arch.persistence.room:compiler:1.1.1'
    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'
    androidTestImplementation "android.arch.core:core-testing:1.1.1"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'nl.littlerobots.rxlint:rxlint:1.6.1'
    
    implementation 'com.dampcake:gson-immutable:1.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation 'org.threeten:threetenbp:1.3.7'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation "org.sagebionetworks.bridge:test-utils:$bridge_sdk_version"

    androidTestImplementation "org.sagebionetworks.bridge:test-utils:$bridge_sdk_version"
    androidTestImplementation 'org.mockito:mockito-android:2.22.0'
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'
    androidTestImplementation'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'org.mockito:mockito-android:2.21.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1"

    // firebase
    implementation 'com.google.firebase:firebase-core:16.0.7'
    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    testImplementation 'com.dampcake:gson-immutable:1.2'
    testImplementation 'org.aaronhe:threetenbp-gson-adapter:1.0.2'
}
tasks.withType(JavaCompile) {
    options.compilerArgs += [
            '-XepExcludedPaths:.*/build/generated/.*'
    ]
}
